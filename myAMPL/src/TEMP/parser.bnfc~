-- bnfc -m AMPLasm.bnfc
-- make
-- happy -i ParAMPLasm.y
-- ./TestAMPLasm
comment "--" ;
comment "{--" "--}" ;

layout "=" , ":", "as" , "of"  ;
token UIdent (upper (letter|digit|'_')*) ;

Main  .AMPLCODE ::= HANDLES COHANDLES CONSTRUCTORS DESTRUCTORS PROCESSES FUNCTIONS START ;
---------------------------------------------------------------------------------------------
 -- An AMPL code file consists of a series of specifcations which can be omitted followed by the code at which one starts.
-- The specifications are:
--      Handles: these are handle names associated with a type.  
--        They are hput on output polarity channels and hcased on input polarity channels
--      Cohandles:  these are handle names associated with a type.
--        They are hput on input polarity channels and hcased on output polarity channels
--      Constructors (and number of arguments)
--      Destructors (and number of arguments)
--      Process definitions
--      Function definitions

---------------------------------------------------------------------------------------------


Hand_spec  .HANDLE_SPEC ::= UIdent "=" "{" [Handle] "}" ;
HandName   .Handle ::= UIdent ;
separator HANDLE_SPEC ";" ;
separator nonempty Handle ";";

Constructors. CONSTRUCTORS ::= "%constructors" ":" "{" [STRUCTOR_SPEC] "}" ;
Constructors_none .CONSTRUCTORS ::= ;

Destructors  .DESTRUCTORS ::= "%destructors" ":" "{" [STRUCTOR_SPEC] "}"  ;
Destructors_none .DESTRUCTORS ::= ;

Struct_spec  .STRUCTOR_SPEC ::= UIdent "=" "{" [STRUCT] "}" ;
Struct       .STRUCT ::= UIdent Integer ;
separator STRUCTOR_SPEC ";" ;
separator nonempty STRUCT ";";

Handles  .HANDLES ::= "%handles"  ":" "{" [HANDLE_SPEC] "}" ;
Handles_none .HANDLES ::= ;

Cohandles  .COHANDLES ::= "%cohandles" ":" "{" [HANDLE_SPEC] "}" ;
Cohandles_none .COHANDLES ::= ;

Processes. PROCESSES  ::= "%processes" ":" "{" [PROCESS_SPEC] "}" ;
Processes_none. PROCESSES ::= ;
separator PROCESS_SPEC ";" ;

Process_spec. PROCESS_SPEC ::= Ident "(" [Vars] "|" [Ident] "=>" [Ident] ")" "=" COMS ;

VName  .Vars ::= Ident ;
separator Vars ",";

Functions  .FUNCTIONS ::= "%functions" ":" "{" [FUNCTION_SPEC] "}" ;
Functions_none .FUNCTIONS ::= ;
separator FUNCTION_SPEC ";" ;

Function_spec  .FUNCTION_SPEC ::= Ident "(" [Vars] ")" "=" COMS    ;
 
Start  .START   ::= "%run" CHANNEL_SPEC ":" COMS ;

Channel_specf .CHANNEL_SPEC ::= "("  "|" [Ident] "=>" [Ident] ")";
Channel_spec .CHANNEL_SPEC ::= "(" [CInteger] "=>" [CInteger] ")";

Prog    .COMS  ::= "{" [COM] "}"; 
separator COM ";"  ;
  
AC_STOREf  .COM   ::= "store" Ident;
AC_LOADf   .COM   ::= "load" Ident ;
AC_RET     .COM   ::= "ret" ;
AC_FRET    .COM   ::= "fret" ;
AC_CALLf   .COM   ::= "call" Ident "(" [Ident] ")" ;
AC_INT     .COM   ::= "cInt" CInteger ;
AC_LEQ     .COM   ::= "leq";
AC_ADD     .COM   ::= "add";
AC_MUL     .COM   ::= "mul";  
AC_CONS    .COM   ::= "cons" "(" Integer "," Integer ")" ;
AC_STRUCT  .COM   ::= UIdent "." UIdent ;
AC_STRUCTas.COM   ::= UIdent "." UIdent "(" [Ident] ")" ;
AC_CASEf   .COM   ::= "case" "of"  "{" [LABELCOMS] "}"  ; 
Labelcoms1 . LABELCOMS ::= UIdent "." UIdent ":" COMS ;
Labelcoms2 . LABELCOMS ::= UIdent "." UIdent "(" [Ident] ")" ":" COMS ;
separator COMS ",";
separator LABELCOMS ";";

AC_RECORDf .COM ::= "rec" "of"  "{" [LABELCOMS] "}"  ;
AC_DEST    .COM ::= "dest" Integer Integer ;
AC_GETf    .COM ::= "get" Ident ; 
AC_HPUTf   .COM ::= "hput" Ident  UIdent "." UIdent;
AC_HCASEf  .COM ::= "hcase" Ident "of"  "{" [LABELCOMS] "}"  ; 
AC_PUTf    .COM ::= "put" Ident ;
AC_SPLITf  .COM ::= "split"Ident "into" Ident Ident    ;
AC_FORKf   .COM ::= "fork" Ident "as" "{" Ident "with" [Ident] "as" COMS ";" Ident "with" [Ident] "as" COMS "}" ;
AC_PLUGf   .COM ::= "plug" Ident  "with" "{" "[" [Ident] "]" ":" COMS ";" "[" [Ident] "]" ":" COMS "}" ;
AC_RUNf    .COM ::= "run" Ident "(" [Ident] "|" [Ident] "=>" [Ident] ")" ;
AC_IDF     .COM ::=  Ident ":=" Ident ;


separator nonempty NCInteger "," ;
separator nonempty NIdent "," ;
separator Ident "," ;
separator TRAN "," ;

TranIn11. TRAN ::= "(" Integer "," "IN" "," Integer ")" ;
TranIn12. TRAN ::= "(" Integer "," "OUT" "," Integer ")" ;

Ncinteger .NCInteger ::= CInteger ;
Nident1 .NIdent ::= Ident ;

AC_CLOSEf  .COM ::= "close" Ident ;
AC_HALTf   .COM ::= "halt" Ident ;

Positive   .CInteger ::= Integer ;
Negative   .CInteger ::= "-" Integer ;
separator CInteger "," ;

